<job id="putValue">
	<script language="VBScript" src="util.vbs" />
	<script language="VBScript" src="regUtil.vbs" />
	<script language="VBScript">
		usage = "usage: cscript regPutValue.wsf regpath1 valueName1 value1 type1 ... [regpathN valueNameN valueN typeN]" & vbNewLine _
					& "types: REG_SZ, REG_EXPAND_SZ, REG_BINARY, REG_DWORD, REG_MULTI_SZ, REG_QWORD"
	
		' bail if args not here
		If args.Count = 0 Then
			WriteLineErr usage
			WScript.Quit 25121
		End If

		If Not args.Count Mod 4 = 0 Then
			WriteLineErr usage
			WScript.Quit 25121
		End If

		For v = 0 To args.Count - 4 Step 4			
			ParseHiveAndSubKey args(v), constHive, strSubKey

			if IsNull(constHive) Then
				WriteLineErr "unsupported hive " & args(v)
				WScript.Quit 25122       
			End If

			strValueName = args(v + 1)
			strValue = args(v + 2)
			strType = args(v + 3)
	
			Result = PutValue(constHive, strSubKey, strValueName, strValue, strType)

			If Not Result = 0 Then
				WriteLineErr "error while putting value"			
				WScript.Quit Result
			End If	
		Next
	</script>
</job>